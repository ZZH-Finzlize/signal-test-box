%locations

%{
#include <stdio.h>
#include <math.h>
#include <stdlib.h>
#include "ast.h"
#include <QtCore>
#include <QMap>
#include <QMessageBox>
#include "symTable.h"
int yyerrorCount = 0;
%}

%code requires
{
#include "ast.h"
#define inFileLoc(arg) arg.first_line, arg.first_column, arg.last_line, arg.last_column
extern int yylex (void);
extern int yyerror(char* s);
inline int yyerror(const char* s) {return yyerror(const_cast<char*>(s));}//内联重载yyerror,消除const char*类型参数警告
extern void resetParser();
extern size_t yycolumn;
extern int yylineno;
extern int yyerrorCount;
}

%union{
    ASTExpress_t *node;//语法树节点
    QList<ASTExpress_t*> *list;
    float value;//浮点数值
    char* id;//符号的名称
}

%token <value> tk_Number "number"
%token <id> tk_Symbol "symbol"

%token tk_Time "time"

%token tk_kwIf "if"
%token tk_kwEles "else"

%token tk_opGequ ">="
%token tk_opLequ "<="
%token tk_opNequ "!="

//优先级低
%left '^'
%left '<' '>' ">=" "<="
%left '+' '-'
%left '*' '/' '%'
/* %right '!' '~' */
//优先级高

/* %type <value> constant_exp */
%type <node> signal signal_exp signal_call func_call
%type <list> arg_list


%%

signal: signal_exp {root = $1;}

signal_exp: '(' signal_exp ')' {$$ = $2;}
    |signal_exp '+' signal_exp {$$ = new ASTOperator_t('+', $1, $3);}
    |signal_exp '-' signal_exp {$$ = new ASTOperator_t('-', $1, $3);}
    |signal_exp '*' signal_exp {$$ = new ASTOperator_t('*', $1, $3);}
    |signal_exp '/' signal_exp {$$ = new ASTOperator_t('/', $1, $3);}
    |signal_exp '%' signal_exp {$$ = new ASTOperator_t('%', $1, $3);}
    |signal_exp '^' signal_exp {$$ = new ASTOperator_t('^', $1, $3);}
    |signal_exp '>' signal_exp {$$ = new ASTOperator_t('>', $1, $3);}
    |signal_exp '<' signal_exp {$$ = new ASTOperator_t('<', $1, $3);}
    |func_call {$$ = $1;}
    |signal_call {$$ = $1;}
    |"number" {$$ = new ASTNumber_t($1);}
    |"time" {$$ = new ASTTime_t();}
    /* |constant_exp */

signal_call: "symbol" {
    $$ = nullptr;
    QListWidgetItem* tmp;
    bool res = SigSymTable.search($1, tmp);

    if(true == res)
    {
        $$ = new ASTSignal_t($1);
    }
    else
    {
        $$ = nullptr;
        yyerror("undefind signal");
    }

    free($1);
}

func_call: "symbol" '(' arg_list ')' {
    ASTFunctionCall_t::calFunc_t find_func = nullptr;
    bool res = FunSymTable.search($1, find_func);
    free($1);
    if(true == res)
    {
        $$ = new ASTFunctionCall_t(find_func, $3);
    }
    else
    {
        $$ = nullptr;
        if(nullptr != $3)
        {
            for(auto it: *$3)
                delete it;

            delete $3;
        }
        yyerror("undefind function");
    }
}

arg_list: {$$ = nullptr;}
    |signal_exp {$$ = new QList<ASTExpress_t*>(); $$->append($1);}
    |arg_list ',' signal_exp {$$ = $1; $$->append($3);}

/* constant_exp: "number"
    |constant_exp '+' constant_exp
    |constant_exp '-' constant_exp
    |constant_exp '*' constant_exp
    |constant_exp '/' constant_exp
    |constant_exp '%' constant_exp
    |constant_exp '^' constant_exp */


%%

int yyerror(char* s)
{
    /* printf("error occor:%s : %d.%d-%d.%d\r\n", s, inFileLoc(yylloc)); */
    QMessageBox::critical(nullptr, QString::fromUtf8(s), 
    QString::fromUtf8("位于 %1.%2-%3.%4").arg(yylloc.first_line).arg(yylloc.first_column).arg(yylloc.last_line).arg(yylloc.last_column)
    );
    yyerrorCount++;
    return 0;
}

void resetParser()
{
    yylineno = 1;
    yycolumn = 1;
    yyerrorCount = 0;
}
